/// \file  wetbulb_accl.cm
/// \brief Accelerated wetbulb calculation. Uses temperature
///	   where h(TDb, w) line intersects saturation line 
///	   as an estimated TWb. 
///	   
///        Includes a kluge to force calculation of the estimate before its use as
/// 	   a PREDICT_FROM_LINK for the actual TWb calculation. The NoOp object
///	   fools the parser and sparksetup into thinking that the TDb input
///	   to the wb object depends upon TWb_est.
///
///
///////////////////////////////////////////////////////////////////////////////
/// \par Interface:
///
/// - pAtm            Atmospheric pressure                     [Pa]
/// - TDb             Dry bulb temperature                     [deg_C]
/// - w               Humidity ratio                           [kg_water/kg_dryAir]
/// - TWb             Wet bulb temperature                     [deg_C]
/// 
/// Acceptable input set:  
///     pAtm = 101325, TDb = 20.0, w = 0.002
///		resulting in TWb = 25194.2
///////////////////////////////////////////////////////////////////////////////
/// \par Local variables:  
///
/// - h               Enthalpy                                 [J/kg_dryAir]
/// - TWb_est  Estimated TWb based on projection of h(TDb, w) onto the saturation curve. [deg_C]
/// - TDb_1    TDb - TWb_est. Part of kluge, see above.
/// - TDb_2    TDb_1 + TWb_est = TDb. Part of kluge, see above.
/// 
///////////////////////////////////////////////////////////////////////////////
/// \par Equations:  
///
/// \code
/// (e)   h = air_enthalpy__h( TDb, w )
/// (esat)  TWb_est = air_enthsat_TDb(h, pAtm)
/// (d)   TDb_1 = TDb - TWb_est
/// (s)   TDb_2 = TDb_1 + TWb_est
/// (wb)  TWb = wetbulb_TWb(TDb, w)
/// \endcode
///
///////////////////////////////////////////////////////////////////////////////
/// \par History
/// 
/// - 280207  Edward F. Sowell
///	- Replaced the add/subtract operations with a new pass-through object, NoOp.
/// - 070207 Edward F. Sowell
///     - Initial implementation.
///
///////////////////////////////////////////////////////////////////////////////


PORT	TDb	NOERR "Drybulb temperature" [deg_C] INIT = 20;
PORT	w	"Humidity ratio" [kg_water/kg_dryAir] INIT = 0.002;
PORT	pAtm	"Atmospheric pressure" [Pa] INIT = 101325.0;
PORT 	TWb	"Wet bulb temperature"      [deg_C]  INIT = 10;
PORT	h	NOERR "Enthalpy" [J/kg_dryAir];
PORT	TWb_est NOERR "Wetbulb estimate"  [deg_C];

DECLARE     air_enthsat esat;
DECLARE     air_enthalpy e;
DECLARE     air_wetbulb wb;
DECLARE	    noop  NoOp;

LINK TDb	   INIT = 20.0
		, .TDb
		, e.TDb,  NoOp.y_in ;

LINK w		  INIT = 0.002
		, .w
		, e.w,  wb.w;


LINK TWb_est      [deg_C]      INIT =    10
            , .TWb_est
	    , esat.TDb, NoOp.x_in;


LINK TDb_2 	[deg_C]		     // numerically the same as TDb but symbolically different to fool parser	
		,  NoOp.y_out, wb.TDb;

LINK pAtm      [Pa]        INIT=101325.0
	   , .pAtm
           , esat.pAtm, wb.pAtm;


LINK h      [J/kg_dryAir]          INIT = 5.742796210351193e+004 
	   , .h
           , esat.hSat, e.h;

LINK TWb    [deg_C]  PREDICT_FROM_LINK = TWb_est
	    , .TWb
	    , wb.TWb;

