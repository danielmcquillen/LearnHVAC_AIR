/// \file  air_enthsat.cm
/// \brief Psychrometric relationship between dry bulb temperature and
///        enthalpy at the saturation point. SI units.
///       
/// Based on the 1989 ASHRAE handbook - Fundamentals
/// 
///////////////////////////////////////////////////////////////////////////////
/// \par  Interface variables
///
/// - pAtm            Atmospheric pressure                     [Pa]
/// - TDb             Dry bulb temperature                     [deg_C]
/// - hSat            Enthalpy at saturation                   [J/kg_dryAir]
///
///////////////////////////////////////////////////////////////////////////////
/// \par  Local variables
///
/// - wSat            Humidity ratio at saturation             [kg_water/kg_dryAir]
///
///////////////////////////////////////////////////////////////////////////////
/// \par  Equations
///
/// \code
/// (hm)    wSat = air_humratio( pAtm, Pw )
/// (sp)    Pw   = air_satpress( TDb )
/// (ea)    hSat = air_enthalpy( TDb, wSat )
/// \endcode
///
///
/// \warning TDb preferred as break variable to facilitate specifying an initial
///          predictor. Better choice than Pw that might lead to w -> inf!!!
///
///////////////////////////////////////////////////////////////////////////////
/// \par  History
/// 
/// - 050711 Chadi Maalouf (LEPTAB)
///     - Initial implementation.
/// 
///////////////////////////////////////////////////////////////////////////////


///////////////////////////////////////////////////////////////////////////////
// Interface variables 
///////////////////////////////////////////////////////////////////////////////

PORT pAtm       "Atmospheric pressure"  [Pa] ;
PORT TDb        "Dry bulb temperature"  [deg_C] ;
PORT hSat       "Enthalpy at saturation" [J/kg_dryAir] ;


///////////////////////////////////////////////////////////////////////////////
// Local variables
///////////////////////////////////////////////////////////////////////////////

PORT wSat       NOERR "Humidity ratio at saturation" [kg_water/kg_dryAir]
                INIT = 0.002 ;
PORT pVapSat    NOERR "Saturated water vapor pressure" [Pa];


///////////////////////////////////////////////////////////////////////////////
// Equations
///////////////////////////////////////////////////////////////////////////////

DECLARE  air_satpress   sp ;
DECLARE  air_enthalpy   ea ;
DECLARE  air_humratio   hm;


LINK    .pAtm       , hm.pAtm;
LINK    pVapSat       
                    , .pVapSat
                    , hm.pVapWat
                    , sp.p;
LINK    wSat
                    , .wSat
                    , ea.w
                    , hm.w;
LINK    .TDb        , sp.T
                    , ea.TDb  BREAK_LEVEL=6;
LINK    .hSat       , ea.h;

