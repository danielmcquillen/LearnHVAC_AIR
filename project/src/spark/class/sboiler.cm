/// \file  sboiler.cm
/// \brief SPARK problem equivalent to the native EnergyPlus BOILER:SIMPLE 
///        component model.
///
/// \author Dimitri Curtil
/// \data   January 15, 2004
///


PORT Qdot               [W]       "actual load generated by boiler";
PORT Qdot_requested     [W]       "requested load";
PORT Qdot_nominal       [W]       "nominal load capacity";

PORT T_inlet            [C]       "inlet water temperatue";
PORT T_outlet           [C]       "outlet water temperatue";
PORT T_setpoint         [C]       "set point for outlet water temperatue";
PORT T_limit            [C]       "upper limit for outlet water temperatue";

PORT mdot               [kg/s]    "actual mass flow rate through boiler";
PORT mdot_locked        [kg/s]    "resolved branch mass flow rate";
PORT mdot_tol           [kg/s]    "minimum significant mass flow rate"
                        INIT=0.001;
PORT mdot_design        [kg/s]    "design mass flow rate";

PORT cp                 [J/(kg.K)] 
                        INIT=4187.0;

PORT FlowLockFlag       [{0=false,1=true}]    
                        INIT=0;
PORT FlowType           [{1=constant,2=variable}]    
                        INIT=1;

PORT efficiency         [W/W]     "boiler efficiency";
PORT FuelUsed           [W]       "fuel used";
PORT TheorFuelUsed      [W]       "theoretical (stoichiometric) fuel used"  NOERR;

PORT ParasiticElecLoad  [W]       "parasitic electric load (e.g. forced draft fan)";
PORT ParasiticElecPower [W]       "parasitic electrical power (e.g. forced draft fan)";

PORT C1                           "first coefficient of the fuel use/part load curve"
                        INIT=1;
PORT C2                           "second coefficient of the fuel use/part load curve"
                        INIT=0;
PORT C3                           "third coefficient of the fuel use/part load curve"
                        INIT=0;

PORT PLR                [W/W]     "boiler part load ratio" NOERR;
PORT PLR_min            [W/W]     "boiler minimum part load ratio";
PORT PLR_max            [W/W]     "boiler maximum part load ratio";



DECLARE boiler_logic     logic;          // Same logic as in EnergyPlus BOILER:SIMPLE
DECLARE boiler_fuel      fuel;           // Calculates the fuel used using a second order polynomial fit
DECLARE boiler_parasitic parasitic;      // Calculates the parasitic electric load (e.g. forced draft fan)
DECLARE quot             quot_PLR;       // c = a / b used to compute PLR
DECLARE quot             quot_TheorUse;  // c = a / b used to compute TheorFuelUsed
DECLARE bound            bound_PLR;      // y = max(lo, min(hi, x))



LINK .Qdot
     logic.Qdot
     quot_PLR.a
     quot_TheorUse.a;

LINK .Qdot_requested
     logic.Qdot_requested;

LINK .Qdot_nominal
     logic.Qdot_nominal
     quot_PLR.b;

LINK .T_inlet
     logic.T_inlet;
LINK .T_outlet
     logic.T_outlet;
LINK .T_setpoint
     logic.T_setpoint;
LINK .T_limit
     logic.T_limit;

LINK .mdot
     logic.mdot;
LINK .mdot_locked
     logic.mdot_locked;
LINK .mdot_tol
     logic.mdot_tol;
LINK .mdot_design
     logic.mdot_design;
LINK .cp
     logic.cp;

LINK .FlowLockFlag
     logic.FlowLockFlag;
LINK .FlowType
     logic.FlowType;

LINK .efficiency
     quot_TheorUse.b;

LINK .FuelUsed
     fuel.FuelUsed;

LINK TheorFuelUsed
     .TheorFuelUsed        // = Qdot / Efficiency
     fuel.TheorFuelUsed
     quot_TheorUse.c;

LINK .C1
     fuel.C1;
LINK .C2
     fuel.C2;
LINK .C3
     fuel.C3;

// Bound PLR_min <= PLR <= PLR_max
LINK .PLR_min
     bound_PLR.lo;
LINK .PLR_max
     bound_PLR.hi;

LINK quot_PLR
     bound_PLR.x     // y = max(lo, min(hi, x))
     quot_PLR.c      // c = a / b
;

LINK .PLR
     bound_PLR.y
     fuel.PLR
     parasitic.PLR;

LINK .ParasiticElecLoad
     parasitic.ParasiticElecLoad;
LINK .ParasiticElecPower
     parasitic.ParasiticElecPower;

