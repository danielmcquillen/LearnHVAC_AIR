/*+++
Identification: Cooling tower model.

Abstract: A model of a direct contact cooling tower.  Based on the DOE-2
          model which employs the "tower unit" concept for normalizing
          empirical performance characteristics.  The basic idea is that
          the size of the tower is expressed in terms of heat transfer
          surface area normalized by the area required to cool one
          gallon per minute from 90F to 80F when the outside wet bulb is
          70F.  An empirical correlation then relates performance to wet
          bulb, water temperature reduction (the range), and the leaving
          differe nce between wet bulb and water temperature (approach).

Notes:    A "soft"  unit conversion has been done to allow SI at the
          interface.

          The model implemented here assumes continuously variable air
          flow rate rather than using the DOE-2 method of viewing the
          tower as divided into cells that are turned on or off to match
          the tower load to the chiller heat rejection. Consequently,
          fan electrical power is simply EIR times the load on the
          tower,  and the only reason for using this model is to
          calculate tower leaving water temperature.

          The DOE-2 correlation is suspicious. Plots of Fr vs. range
          don't look much like those in the DOE-2 Engineering Manual. It
          could be that the curve fits are not good.

Interface:
      PAtm:     Atmosperic pressure (Pa)
      mAirRated:Rated air flow (kg dry air/s)
      mAir:     Air flow (kg dry air/s)
      mLiq:     Water flow (kg/s)
      TAmb:     Ambiant (outside) air dry bulb temperature (deg C)
      wAmb:     Ambient air humidity  ratio (kg-water/kg dry air)
      TLiqLvg:  Leaving water temperature (deg C)
      TLiqEnt:  Entering water temperature (deg C)
      Ar:       Tower size in tower units. (-)
      p:        Exponent on air mass flow rate correlation (-)
      EIRfan:   Fan power per unit load (-)
      deltaH:   Water pump head (m)
      Effmot:   Water pump motor efficiency (-)
      Effpump:  Water pump efficiency (-)
      qTot:     Total heat transfer rate.  Positive for water cooling. (W)
      power:    Power consumed by unit (W)

Acceptable input set:
      PAtm = 101325, TAmb 29.44, TWb = 15.56,
      qTot = 35172, mAirRated = 1, mAir = 1, mLiq = 1,
      p = 1, TLiqEnt = 30,
      EIRfan = 0.1, deltaH = 3, g = 9.81,
      Effmot 0.85, Effpump = 0.6

Suggested breaks:
??EE??      wb.wSStar = 0.010 BREAK_LEVEL = 9

Local variables:
      TWb:   Ambiant (outside) air wet bulb temperature (deg C)
      m_bar: Air mass flow normalized by design value. (-)
      Fr:    Rating factor (s/kg)
      app:   Approach temperature difference of leaving water to wet bulb (deg C)
      R:     Range (water temperature drop) (C deg)
      pumpP: Pump power (W)
      fanP:  Fan power (W)
      g:     Acceleration of gravity (9.81 m/s^2)
      capLiq:Water flow capacity rate (W/deg-C)

Equations:
      wetbulb(TAmb, wAmb, TWb)
      m_bar = mAir/mAirRated
      mLiq * Fr = m_bar^p * Ar
      Fr = f(app, R, TWb)   (an empirical correlation)
      app = TLiqLvg - TWb
      R = TLiqEnt - TLiqLvg
      capLiq = mLiq * cpWater
      Q =  capLiq * R
      pumpP = g * deltaH * mLiq/(effMot * effPump)
      fanP = EIRfan * q
      power = pumpP + fanP
---*/

PORT PAtm    "Atmosperic pressure"        [Pa] ;
PORT mAirRated    "Rated air flow"        [kg_dryAir/s] ;
PORT mAir    "Air flow"            [kg_dryAir/s] ;
PORT mLiq    "Water flow"            [kg/s] ;
PORT TAmb    "Ambiant -outside- air dry bulb temperature"    [deg_C] ;
PORT wAmb    "Ambient air humidity  ratio"    [kg_water/kg_dryAir] ;
PORT TLiqLvg    "Leaving water temperature"    [deg_C] ;
PORT TLiqEnt    "Entering water temperature"    [deg_C] ;
PORT Ar        "Tower size in tower units"    [] ;
PORT p        "Exponent on air mass flow rate correlation"    [] ;
PORT EIRfan    "Fan power per unit load"    [] ;
PORT deltaH    "Water pump head"        [m] ;
PORT Effmot    "Water pump motor efficiency"    [] ;
PORT Effpump    "Water pump efficiency"        [] ;
PORT qTot    "Total heat transfer rate  Positive for water cooling"    [W] ;
PORT power    "Power consumed by unit"    [W] ;

PORT g        /*EE*/ ;
PORT TWb    /*EE*/ ;


DECLARE prod    sp1, sp2, sp3, sp4 ;
DECLARE quot    sq1, sq2, sq3, sq4, sq5 ;
DECLARE sum    s1, s2, s3 ;
DECLARE pow    pow ;
DECLARE ctfunc    ctf ;
DECLARE wetbulb    wb ;
  PROBE wb_P1    wb~L_wSStar    BREAK_LEVEL = 9  INIT = 0.010 ;
DECLARE capratel    capl ;


LINK        .PAtm,    wb.PAtm                [Pa] ;
LINK        .TAmb,    wb.TDb                [deg_C] ;
LINK        .wAmb,    wb.w                [kg_water/kg_dryAir] ;
LINK        .qTot,    sq1.a, sp1.b            [W] ;
LINK        .mAirRated,    sq2.b            [kg_dryAir/s] ;
LINK        .mAir,    sq2.a                [kg_dryAir/s] ;
LINK        .mLiq,    sq3.b, capl.mWater, sp4.b    [mwdt] ;
LINK        .Ar,    sp2.b ;
LINK        .p,    pow.b ;
LINK        .EIRfan,    sp1.a ;
LINK        .deltaH,    sp3.b ;
LINK        .g,        sp3.a ;

LINK        .Effmot,    sq4.b ;
LINK        .Effpump,    sq5.b ;

LINK capLiq    capl.cap, sq1.b ;
LINK R        ctf.R, s1.a, sq1.c ;
LINK        .TWb,    ctf.TWb, s2.b, wb.TWb        [deg_C] ;
LINK m_bar    sq2.c, pow.a ;
LINK v1        pow.c, sp2.a ;
LINK v2        sp2.c, sq3.a ;
LINK Fr        sq3.c, ctf.Fr ;
LINK app    ctf.app, s2.a ;
LINK        .TLiqLvg,    s2.c, s1.b        [deg_C] ;
LINK        .TLiqEnt,    s1.c            [deg_C] ;
LINK fanP    sp1.c, s3.b                [W] ;
LINK v3        sp3.c, sp4.a ;
LINK v4        sp4.c, sq4.a ;
LINK v5        sq4.c, sq5.a ;
LINK pumpP    sq5.c, s3.a                [W] ;
LINK        .power,    s3.c                [W] ;
