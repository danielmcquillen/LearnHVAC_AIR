/// \file htxc1u.cm
/// \brief  Cross flow, stream 1 unmixed heat exchanger model
/// 
///   Abstract:        
///        An Ntu-effectiveness heat exchanger model for the cross flow
///           arrangement with stream 1 unmixed. Unlike heatex.cm,  this
///           model applies to this single flow arrangement. This can be
///           changed by editing comment delimiters for the effectiveness
///           object in this file.
/// 
///   Uses: effc1u.cc
/// 
///   Notes:           
///        The advantage of this object is that it can be used in design
///          situations, e.g., find UA given performance data, as well as
///          the normal simulation, often without introducing iteration
///          variables.
/// 
///   Interface:       
/// -      cap1:    Capacity flow, stream 1. (W/C-deg)
/// -      cap2:    Capacity flow, stream 2. (W/C-deg)
/// -      TEnt1:    Inlet temperature, stream 1. (deg C)
/// -      TEnt2:    Inlet temperature, stream 2. (deg C)
/// -      TLvg1:   Outlet temperature, stream 1.(deg C)
/// -      TLvg2:   Outlet temperature, stream 2.(deg C)
/// -      q:       Heat flow;  Positive for stream 1 -> stream 2.(W)
/// -      UA:      UA (W/C-deg)
/// 
///   Acceptable input set:  
///        cap1 = 1000, cap2 = 1000, UA = 100, TEnt1 = 20, TEnt2 = 10
/// 
///   Recommended matches:  
///           None
/// 
///   Suggested breaks:  
///           None
/// 
///   Local variables:  
/// -      qRef:     Reference heat transfer rate (W)
/// -      effp:     Reference heat exchanger effectiveness, q/qRef (fraction)
/// -      cRatiop:  Reference capacity ratio, cap1/cap2 (dimless)
/// 
///   Equations:  
/// -      ntup = UA/cap1
/// -      cRatiop = cap1/cap2
/// -      effect(cRatiop, ntup, effp)
/// -      qRef = cap1*(TEnt1-TEnt2)
/// -      q = cap1*(TEnt1-TLvg1)
/// -      q = cap2*(TLvg2-TEnt2)
/// -      q = effp * qRef


PORT cap1    "Capacity flow, stream 1"                         [W/deg_C] ;
PORT cap2    "Capacity flow, stream 2"                         [W/deg_C] ;
PORT TEnt1    "Inlet temperature, stream 1"                     [deg_C] ;
PORT TEnt2    "Inlet temperature, stream 2"                     [deg_C] ;
PORT TLvg1    "Outlet temperature, stream 1"                    [deg_C] ;
PORT TLvg2    "Outlet temperature, stream 2"                    [deg_C] ;
PORT q      "Heat flow;  Positive for stream 1 -> stream 2"    [W] NOERR;
PORT UA        "UA"                                             [W/deg_C] ;


DECLARE prod    prod ;
DECLARE quot    quot1, quot2 ;
DECLARE cond    cd1, cd2, cd3 ;
/* DECLARE effctr    effect ;    /* COUNTER FLOW */
/* DECLARE effprl    effect ;    /* PARALLEL FLOW */
/* DECLARE effcbu    effect ;    /* CROSS FLOW BOTH UNMIXED */
/* DECLARE effcbm    effect ;    /* CROSS FLOW BOTH MIXED */
DECLARE effc1u    effect ;    /* CROSS FLOW FIRST UNMIXED */


LINK        .cap1,    quot1.b, quot2.a, cd1.U12, cd2.U12    [W/deg_C] ;
LINK        .cap2,    quot2.b, cd3.U12                    [W/deg_C] ;
LINK        .UA,    quot1.a                                [W/deg_C] ;
LINK        .TEnt1,    cd1.T1, cd2.T1                        [deg_C] ;
LINK        .TEnt2,    cd1.T2, cd3.T2                        [deg_C] ;

LINK ntup    quot1.c, effect.ntup ;
LINK cRatiop    quot2.c, effect.cRatiop ;
LINK effp    effect.effp, prod.b                            [-] ;
LINK qRef    cd1.q, prod.a                                [W] ;
LINK        .TLvg1,    cd2.T2                                [deg_C] ;
LINK        .TLvg2,    cd3.T1                                [deg_C] ;
LINK        .q,    prod.c, cd2.q, cd3.q                    [W] ;
