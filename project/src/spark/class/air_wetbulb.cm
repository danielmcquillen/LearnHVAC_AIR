/// \file  air_wetbulb.cm
/// \brief Wet Bulb (Hyland & Wexler)
/// 
/// Psychrometric relationship between temperature, humidity
/// ratio, and wet bulb temperature. SI units. Ref: Eqs. 21, 31,
/// 32 of ASHRAE HOF 1985,  SI units.
/// 
/// \note Changed to new satpress relationship based on later ASHRAE HOF
/// 
///////////////////////////////////////////////////////////////////////////////
/// \par Interface:       
///
/// - pAtm            Atmospheric pressure                     [Pa]
/// - TDb             Dry bulb temperature                     [deg_C]
/// - w               Humidity ratio                           [kg_water/kg_dryAir]
/// - TWb             Wet bulb temperature                     [deg_C]
/// - wSStar          Humidity ratio star                      [kg_water/kg_dryAir]
///
/// Acceptable input set:  
///     pAtm = 101325, TDb = 20.0, w = 0.010
/// 
/// Recommended matches:  
///     None
/// 
/// Suggested breaks:  
///     TWb = 20 BREAK_LEVEL = 7
///     wSStar = 0.010 BREAK_LEVEL = 7
/// 
///////////////////////////////////////////////////////////////////////////////
/// \par Local variables:  
///
/// - h               Enthalpy                                 [J/kg_dryAir]
/// - hSStar          Enthalpy star                            [J/kg_dryAir]
/// - PwSStar         Sat. partial pressure water vapor, star  [Pa]
/// 
///////////////////////////////////////////////////////////////////////////////
/// \par Equations:  
///
/// \code
/// (e1)   h = enthalpy( TDb, w )
/// (e2)   hSStar = enthalpy( TWb, wSStar )
/// (hr)   wSStar = humratio( PwSStar, pAtm )
/// (sp)   PwSStar = satp_hw( TWb )
/// (eq31) hSStar = eq31( h, w,  TWb, wSStar )
/// \endcode
///
///////////////////////////////////////////////////////////////////////////////
/// \par History 
/// - Implemented by EFS 2/7/2007 based on the original wetbulb.cm in hvactk directory.
///   Only difference is to use the more recent psychrometric classes in hvactk2.
///
///////////////////////////////////////////////////////////////////////////////
 


PORT pAtm   "Atmospheric pressure"  [Pa] ;
PORT TDb    "Dry bulb temperature"  [deg_C] ;
PORT w      "Humidity ratio"        [kg_water/kg_dryAir] ;
PORT TWb    "Wet bulb temperature"  [deg_C]
            INIT=20
           BREAK_LEVEL=7 ;
PORT wSStar "Humidity ratio star"   [kg_water/kg_dryAir]
            NOERR
            INIT=0.010
            BREAK_LEVEL=7;


DECLARE air_enthalpy    e1, e2 ;   //  h = air_enthalpy( TDb, w )
DECLARE air_humratio    hr ;       //  w = air_humratio( pVapWat, pAtm )
DECLARE air_satpress     sp ;       //  P = air_satpress( T )
DECLARE air_eq31        eq31 ;     //  hSStar = eq31( h, w,  TWb, wSStar )


LINK  pAtm  	.pAtm,      hr.pAtm;
LINK  TDb  	.TDb,       e1.TDb;
LINK   w   	.w,         e1.w,eq31.w;
LINK   wSStar 	.wSStar,    eq31.wSStar, e2.w, hr.w;
LINK   TWb	.TWb,       eq31.TWb  , e2.TDb, sp.T;

LINK    h           e1.h, eq31.h        "Enthalpy" [J/kg_dryAir] ;
LINK    hSStar      eq31.hSStar, e2.h   "Enthalpy star" [J/kg_dryAir] ;
LINK    PwSStar     hr.pVapWat, sp.p         "Sat. partial pressure water vapor, star" [Pa] ;
