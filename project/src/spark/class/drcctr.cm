/// \file drcctr.cm
///
///  Identification:  Dry coil, counter flow
///
///  Abstract:        
///       An air-liquid heat exchanger with sensible only heating or
///          cooling.  Can be used in conjunction with the wet coil model
///          to simulate a partially dry cooling/dehumidification coil.
///          Based on the ASHRAE Toolkit DRYCOIL model. Liquid is assumed
///          to be water.
///
///  Notes:           
///       The configuration is counter flow
///
///  Interface:       
///      mAirEnt:  Air flow (kg dry air/s)
///      mLiq:     Liquid flow (kg/s)
///      TAirEnt:  Entering air dry bulb temperature (deg-C)
///      TLiqEnt:  Entering water temperature (deg C)
///      wAirEnt:  Entering air humidity  ratio (kg-water/kg dry air)
///      UA:       Overall heat transfer coefficient (W/deg C)
///      mAirLvg:  Leaving air flow (kg dry air/s)
///      wAirLvg:  Leaving air humidity  ratio (kg-water/kg dry air)
///      TAirLvg:  Leaving air Temperature (deg C)
///      TLiqLvg:  Leaving water temperature (deg C)
///      q:        Heat transfer rate.  Positive for air cooling. (W)
///
///  Acceptable input set:  
///       UA = 1000, mAirEnt = 1, mLiq = 1, TAirEnt = 15, wAirEnt = 0.001,
///       TLiqEnt = 50
///
///  Local variables:  
///      capAir:   Air capacity rate (kg/s)
///      capLiq:   Water capacity rate (kg/s)
///
///  Equations:  
///      capAir = mAirEnt*(CpAir+WAirEnt*CpVap)
///      capLiq = MLiq*CpLiq
///      heatex(capLiq,TLiqEnt,capAir,TAirEnt,UA,ConfigHX,TLiqLvg,TAirLvg)
///      Q = capAir*(TAirEnt-TAirLvg)
///      WAirLvg = WAirEnt


PORT mAirEnt    "Air flow"                [kg_dryAir/s] ;
PORT mLiq    "Liquid flow"                [kg_water/s] ;
PORT TAirEnt    "Entering air dry bulb temperature"    [deg_C] ;
PORT TLiqEnt    "Entering water temperature"        [deg_C] ;
PORT wAirEnt    "Entering air humidity  ratio"        [kg_water/kg_dryAir] ;
PORT UA        "Overall heat transfer coefficient"    [W/deg_C] ;
PORT mAirLvg NOERR  "Leaving air flow"            [kg_dryAir/s] ;
PORT wAirLvg    "Leaving air humidity  ratio"        [kg_water/kg_dryAir] ;
PORT TAirLvg    "Leaving air Temperature"        [deg_C] ;
PORT TLiqLvg NOERR    "Leaving water temperature"    [deg_C] ;
PORT q        "Heat transfer rate.  Positive for air cooling."    [W] ;


DECLARE cap_rate    cra ;
DECLARE capratel    crl ;
DECLARE htxctr    ex ;    /* COUNTER FLOW */
/* DECLARE htxprl    ex ;    /* PARALLEL FLOW */
/* DECLARE htxcbu    ex ;    /* CROSS FLOW BOTH UNMIXED */
/* DECLARE htxcbm    ex ;    /* CROSS FLOW BOTH MIXED */
/* DECLARE htxc1u    ex ;    /* CROSS FLOW FIRST UNMIXED */
DECLARE cond    cd ;
DECLARE equal    eq1, eq2 ;

LINK        .UA,        ex.UA            [W/deg_C] ;
LINK        .mAirEnt,    cra.mAir, eq1.a        [kg_dryAir/s] ;
LINK        .mAirLvg,    eq1.b            [kg_dryAir/s] ;
LINK        .mLiq,        crl.mWater        [kg_water/s] ;
LINK        .TAirEnt,    ex.TEnt2, cd.T1        [deg_C] ;
LINK        .wAirEnt,    cra.w, eq2.a        [kg_water/kg_dryAir] ;
LINK        .TLiqEnt,    ex.TEnt1        [deg_C] ;

LINK  capAir    cra.cap, ex.cap2, cd.U12        [W/deg_C] ;
LINK  capLiq    crl.cap, ex.cap1            [W/deg_C] ;
LINK        .TAirLvg,    ex.TLvg2, cd.T2        [deg_C] ;
LINK        .TLiqLvg,    ex.TLvg1        [deg_C] ;
LINK        .wAirLvg,    eq2.b            [kg_water/kg_dryAir] ;
LINK        .q,        cd.q            [W] ;
