/// \file sat_pressure.cm
/// 
/// Used to compute evaporating and condensing pressures by means of the Clausius-clapeyron equation
///
/// p = 1000 * exp( Acl + Bcl/T )
///

PORT p   [Pa]  "pressure";
PORT T   [K]   "temperature";
PORT Acl [-]   "first coefficient in Clausius-Clapeyron equation";
PORT Bcl [K]   "second coefficient in Clausius-Clapeyron equation";
PORT THOUSAND  DEFAULT=1000;

DECLARE quot      quot1;  // c = a / b
DECLARE sum       sum1;   // c = a + b
DECLARE log       log1;   // c = log( a )
DECLARE prod      prod1;  // c = a * b

LINK .Acl
     sum1.a;
LINK .Bcl
     quot1.a;
LINK .T
     quot1.b;
LINK temp_BT,    // Bcl/T
     sum1.b,
     quot1.c;
LINK temp_ApBT,  // Acl + Bcl/T
     sum1.c,
     log1.b;
LINK temp_exp,   //  exp( Acl + Bcl/T )
     log1.a
     prod1.b;
LINK .THOUSAND
     prod1.a;
LINK .p
     prod1.c;

