<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:myComps="*"
	horizontalScrollPolicy="off"
	verticalScrollPolicy="off"
	width="670" 
	height="500"
	creationComplete="onCreationComplete()"
	>
	
	<mx:Script>
		<![CDATA[
		
			// NOTE: For some reason databinding isn't working with systemVariable.baseSIValue...
			//       so I'm binding to currValue (which does work) and grabbing SI value manually
			// 		 This isn't as efficiency but since we're only doing this a couple times 
			//       per update the cost shouldn't be too significant
		
			import flash.geom.ColorTransform
			import com.mcquilleninteractive.learnhvac.util.Logger
			import mx.binding.utils.BindingUtils
			import com.mcquilleninteractive.learnhvac.model.LHModelLocator
			import com.mcquilleninteractive.learnhvac.model.ScenarioModel
			import com.adobe.cairngorm.control.CairngormEventDispatcher
   			import com.mcquilleninteractive.learnhvac.event.ScenarioLoadedEvent
			import com.mcquilleninteractive.learnhvac.event.AHUEvent
									
			public var scenModel:ScenarioModel
			public var _active:Boolean = false
			
			public function onCreationComplete():void
			{
				var dispatcher:CairngormEventDispatcher = CairngormEventDispatcher.getInstance()
   				dispatcher.addEventListener(ScenarioLoadedEvent.SCENARIO_LOADED, scenarioLoaded)
   				dispatcher.addEventListener(AHUEvent.EVENT_AHU_STARTED, onAHUOn)
				dispatcher.addEventListener(AHUEvent.EVENT_AHU_STOPPED, onAHUOff)
			
				/*
   				if (LHModelLocator.getInstance().scenarioModel.scenID!=null)
   				{
   					initBindings()
   				}
   				*/
			}
			
			public function scenarioLoaded(e:Event):void
			{	
				initBindings()
			}
			
			public function initBindings():void
			{
				scenModel = LHModelLocator.getInstance().scenarioModel
				
				BindingUtils.bindSetter(setTLiqEntValve, scenModel.getSysVar("CCTLiqEntValve"), "currValue" )
				BindingUtils.bindSetter(setTLiqLvg, scenModel.getSysVar("CCTLiqLvg"), "currValue" )
				BindingUtils.bindSetter(setmLiqEnt, scenModel.getSysVar("CCmLiqEnt"), "currValue" )
			}	
				
			public function onAHUOn(event:AHUEvent):void
			{
				if (_active)
				{
					updatePipes()
				}
			}
			
			public function onAHUOff(event:AHUEvent):void
			{
				liquidsCC.turnAllPipesOff()
			}
			
			public function updatePipes():void
			{
				try
				{
					setTLiqEntValve(scenModel.getSysVar("CCTLiqEntValve").baseSIValue)
					setTLiqLvg(scenModel.getSysVar("CCTLiqLvg").baseSIValue)
					setmLiqEnt(scenModel.getSysVar("CCmLiqEnt").baseSIValue)
				}					
				catch(e:Error)
				{
					Logger.error("#PipeLiquidsCC: couldn't set a field value when trying to updatePipes()")
				}	
			}
				
			public function set active(state:Boolean):void
			{
				_active = state
				this.visible = state
				if (state)
				{
					//user has entered system node where this component should be visible
					//get latest current values if AHU is on
					
					if (LHModelLocator.getInstance().scenarioModel.ahuStatus == ScenarioModel.AHU_ON)
					{
						updatePipes()				
					}
				}
				else
				{
					//turn off component, no longer visible
					liquidsCC.turnAllPipesOff()
				}
			}
					
					
			public function setTLiqEntValve(value:Number):void
			{
				if (!_active) return
				
				//get SI value manually -- see note above
				value = scenModel.getSysVar("CCTLiqEntValve").baseSIValue
				liquidsCC.setTemp(value, 0)
				liquidsCC.setTemp(value, 1)
			}			
					
			public function setTLiqLvg(value:Number):void
			{
				if (!_active) return
				
				//get SI value manually -- see note above
				value = scenModel.getSysVar("CCTLiqLvg").baseSIValue
				
				liquidsCC.setTemp(value, 2)
				liquidsCC.setTemp(value, 3)
			}
					
			public function setmLiqEnt(value:Number):void
			{
				if (!_active) return
				
				//get SI value manually -- see note above
				value = scenModel.getSysVar("CCmLiqEnt").baseSIValue
				
				liquidsCC.setFlow(value, 0)
				liquidsCC.setFlow(value, 1)
				liquidsCC.setFlow(value, 2)
				liquidsCC.setFlow(value, 3)
			}
			
				
			
		]]>
	</mx:Script>
	
	<myComps:LiquidsCC id="liquidsCC" />
	
</mx:Canvas>
