<?xml version="1.0" encoding="utf-8"?>
<mx:HBox 	xmlns:mx="http://www.adobe.com/2006/mxml" 
			height="105" 
			paddingLeft="10"  
			paddingTop="10" borderSides="bottom" borderColor="#888888" borderThickness="5"
			initialize="onInitialize()"
			>
    
    <mx:Script>
    	<![CDATA[
    		import com.mcquilleninteractive.learnhvac.model.LHModelLocator;
    		import com.mcquilleninteractive.learnhvac.vo.ScenarioListItemVO
    		import com.adobe.cairngorm.control.CairngormEvent;
			import com.adobe.cairngorm.control.CairngormEventDispatcher;
    		import com.mcquilleninteractive.learnhvac.util.Logger;
    		import com.mcquilleninteractive.learnhvac.event.LoadScenarioEvent;
    		import com.mcquilleninteractive.learnhvac.event.GetScenarioListEvent;
    		
    		[Embed(source='/assets/img/pic_default_scenario.png')]
    		[Bindable]
    		public var defaultScenarioPic:Class
    		
    		public function onInitialize():void
    		{	
    			Logger.debug("#SelectScenarioItem: registering listener")
				CairngormEventDispatcher.getInstance().addEventListener(GetScenarioListEvent.EVENT_SCENARIO_LIST_LOADED, onScenarioListLoaded, false, 0,true)
    		}
    		
    		public function loadScenario():void
    		{
    			Logger.debug("#SelectScenarioItem: loading scenario")
    			var model:LHModelLocator = LHModelLocator.getInstance()
    			var type:String
    			var fileName:String = ""
    			
    			var scenarioListItemVO:ScenarioListItemVO = ScenarioListItemVO(data)
    			
    			var cgEvent:LoadScenarioEvent
    			switch (scenarioListItemVO.sourceType)
    			{
    				case ScenarioListItemVO.SOURCE_LOCAL_FILE:
	    				cgEvent =  new LoadScenarioEvent(LoadScenarioEvent.EVENT_LOAD_LOCAL_SCENARIO)
	    				cgEvent.fileName = scenarioListItemVO.fileName
    					break
    					
    				case ScenarioListItemVO.SOURCE_REMOTE:
    					cgEvent =  new LoadScenarioEvent(LoadScenarioEvent.EVENT_LOAD_REMOTE_SCENARIO)
    					cgEvent.scenID = scenarioListItemVO.scenID
    					break
    					
    				case ScenarioListItemVO.SOURCE_DEFAULT:
    					cgEvent =  new LoadScenarioEvent(LoadScenarioEvent.EVENT_LOAD_SCENARIO_XML)
    					cgEvent.xml = scenarioListItemVO.scenarioXML
    					break
    					
    				default:
    					Logger.error("Unrecognized scenarioListItem type: " + scenarioListItemVO.sourceType)
    					
    			}
    			
    			
    			CairngormEventDispatcher.getInstance().dispatchEvent(cgEvent)
    		}
    		
    		override public function set data( value:Object ) : void 
    		{
				super.data = value;
				if (value && value.thumbnail_URL==null)
				{
					scenarioThumbnail.source = new defaultScenarioPic()
				}
				else
    			{    				
					scenarioThumbnail.load()
    			}
			}

    		
    		public function onScenarioListLoaded(event:GetScenarioListEvent):void
    		{
    			Logger.debug("#SelectScenarioItem: trying to load scenarioThumbnail...")
    			scenarioThumbnail.load()
    		}
    		
    		protected function showDefaultImage():void
    		{
    			Logger.debug("#SelectScenarioItem: couldn't find pic at : " + data.thumbnail_URL + " so using default image")
    			scenarioThumbnail.source = defaultScenarioPic
    			scenarioThumbnail.invalidateProperties()
    		}

    		
    		
    	]]>
    </mx:Script>
    
    <mx:VBox 	width="125"
         		height="90" 
         		horizontalAlign="center"
         		verticalAlign="middle">
        <mx:Canvas width="125" height="90">
        
        		<mx:Image id="scenarioThumbnail" 
		    		source="{data.thumbnail_URL}"
		    		height="90" width="125" 
		    	    ioError="showDefaultImage()"
		    		 />
        </mx:Canvas>
    
    </mx:VBox>
    
    <mx:VBox >
        <mx:Label text="{data.name}" styleName="itemHeader" />
		<mx:Text text="{data.short_description}" width="400" height="35" />
		<mx:Button label="Start Scenario" width="120" height="25" click="loadScenario()"/>
	</mx:VBox>

</mx:HBox>
	